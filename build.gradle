buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        maven { url 'https://plugins.gradle.org/m2/' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.1.+', changing: true
        classpath group: 'gradle.plugin.com.matthewprenger', name: 'CurseGradle', version: '1.4.0'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'com.matthewprenger.cursegradle'

version = "${mod_version}"
group = "${mod_group}"
archivesBaseName = "${mod_id}-forge"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: 'snapshot', version: "${version_mcp}".toString()

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            mods {
                champions {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            mods {
                champions {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {

    maven {
        url 'https://maven.blamejared.com/'
    }

    maven {
        url 'https://mmdmaven.blamejared.com/'
    }

    maven {
        name = "CurseMaven"
        url = "https://www.cursemaven.com"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${version_forge}"

    compile fg.deobf("com.blamejared.crafttweaker:CraftTweaker-1.16.5:7.1.0.100")
    compile fg.deobf("net.darkhax.gamestages:GameStages-1.16.4:6.0.4")
    compile fg.deobf("curse.maven:scalinghealth-248027:3170946")

    runtimeOnly fg.deobf("net.darkhax.bookshelf:Bookshelf-1.16.5:10.0.1")
    runtimeOnly fg.deobf("curse.maven:silentlib-242998:3165746")
}

sourceSets {
    main {
        resources {
            srcDirs += 'docs'
        }
    }
}

jar {
    manifest {
        attributes(["Specification-Title": "${mod_name}",
                    "Specification-Vendor": "${mod_author}",
                    "Specification-Version": "${getArchiveVersion()}",
                    "Implementation-Title": "${mod_name}",
                    "Implementation-Version": "${getArchiveVersion()}",
                    "Implementation-Vendor" :"${mod_author}",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")],)
    }
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allJava
}

tasks.build.dependsOn sourcesJar

processResources {

    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'
        expand 'version': project.version, 'mod_id' : mod_id, 'mod_name': mod_name, 'mod_url': mod_url, 'mod_author': mod_author, 'mod_description': mod_description, 'mod_icon': mod_icon
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'META-INF/mods.toml'
    }
}


curseforge {

    project {
        apiKey = findProperty('curseKey') ?: 0
        id = "${curse_id}"
        releaseType = "${curse_release}"
        changelogType = 'markdown'
        changelog = file('docs/CHANGELOG.md')
        addArtifact(sourcesJar)
        "${curse_versions}".split(',').each {
            addGameVersion "${it}"
        }
        relations {
            optionalDependency 'game-stages'
            optionalDependency 'crafttweaker'
        }
    }
}
